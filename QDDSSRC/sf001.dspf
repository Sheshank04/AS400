     A*%%TS  SD  20251010  054610  SHESHANK    REL-V7R5M0  5770-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      INDARA
     A                                      CF03(03 'Exit')
     A                                      CF05(05 'Refresh')
     A                                      CF06(06 'Add')
     A                                      CF08(08 'Update')
     A                                      CF12(12 'Cancel')
     A          R HEADER
     A*%%TS  SD  20251010  054610  SHESHANK    REL-V7R5M0  5770-WDS
     A                                  1  8USER
     A                                  1  2'User:'
     A                                  1 34'Programmers.IO'
     A                                      COLOR(WHT)
     A                                  1 66'Date:'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                  2 35'User Details'
     A                                      DSPATR(UL)
     A                                  6  2'Type Options, Press Enter'
     A                                      COLOR(BLU)
     A                                  7  3'2=Edit  4=Delete  5=Display'
     A                                      COLOR(BLU)
     A                                  6 31' '
     A                                  4  2'Position To:'
     A            POSTO          5Y 0B  4 15
     A  70                                  DSPATR(RI)
     A  70                                  DSPATR(PC)
     A                                      EDTCDE(Z)
     A          R USRSFL                    SFL
     A*%%TS  SD  20251009  091024  SHESHANK    REL-V7R5M0  5770-WDS
     A            SUSERID        5Y 0O 11  7EDTCDE(Z)
     A            SUSERNAME     25A  O 11 16
     A            SUSERDEP      10A  O 11 45
     A            SUSERMOBNO    10A  O 11 59
     A            OPT            1A  B 11  2
     A  50                                  DSPATR(RI)
     A  50                                  DSPATR(PC)
     A          R USRSFLCTL                 SFLCTL(USRSFL)
     A*%%TS  SD  20251009  091024  SHESHANK    REL-V7R5M0  5770-WDS
     A                                      SFLSIZ(9999)
     A                                      SFLPAG(0008)
     A                                      OVERLAY
     A  31                                  SFLDSP
     A  32                                  SFLDSPCTL
     A  33                                  SFLCLR
     A  34                                  SFLEND(*MORE)
     A*           RCDNBR         4S 0H      SFLRCDNBR(*TOP)
     A            RCDNBR         4S 0H      SFLRCDNBR(CURSOR)
     A                                 10  2'Opt'
     A                                      COLOR(WHT)
     A                                 10 10'Id'
     A                                      COLOR(WHT)
     A                                 10 16'Name'
     A                                      COLOR(WHT)
     A                                 10 45'Department'
     A                                      COLOR(WHT)
     A                                 10 59'Mobile No.'
     A                                      COLOR(WHT)
     A          R FOOTER
     A*%%TS  SD  20251009  091024  SHESHANK    REL-V7R5M0  5770-WDS
     A                                      OVERLAY
     A            MSG1          70A  O 24  2COLOR(WHT)
     A                                 23  2'F3=Exit          F5=Refresh       -
     A                                      F6=Add           F12=Cancel'
     A                                      COLOR(BLU)
     A                                 23 64' '
     A          R USRDTL
     A*%%TS  SD  20251010  051842  SHESHANK    REL-V7R5M0  5770-WDS
     A                                      CF04(04 'Prompt')
     A                                      RTNCSRLOC(&RCD &FLD &POS)
     A                                      OVERLAY
     A            FLD           10A  H
     A            RCD           10A  H
     A            POS            4S 0H
     A                                  1  2'User:'
     A                                  1  8USER
     A                                  1 34'Programmers.IO'
     A                                      COLOR(WHT)
     A                                  1 66'Date:'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A            HEADING       20A  O  2 31
     A                                  8 19'User Id.............:'
     A                                      COLOR(WHT)
     A                                 10 19'User Name...........:'
     A                                      COLOR(WHT)
     A                                 12 19'User Department.....:'
     A                                      COLOR(WHT)
     A                                 14 19'User Mobile Number..:'
     A                                      COLOR(WHT)
     A            DUSERNAME     25A  B 10 41
     A  51                                  DSPATR(RI)
     A  51                                  DSPATR(PC)
     A                                      CHECK(LC)
     A  60                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            DUSERDEP      10A  B 12 41
     A  52                                  DSPATR(RI)
     A  52                                  DSPATR(PC)
     A                                      CHECK(LC)
     A                                      DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            DUSERMOBNO    10A  B 14 41
     A  53                                  DSPATR(RI)
     A  53                                  DSPATR(PC)
     A  60                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A            MSG2          70A  O 24  2COLOR(WHT)
     A            FUNFLD        70A  O 23  2COLOR(BLU)
     A            DUSERID        5Y 0O  8 41EDTCDE(Z)
     A                                 12 52'(F4 = Prompt)'
     A  61                                  DSPATR(ND)
     A                                      COLOR(BLU)
     A          R POPUP
     A*%%TS  SD  20251009  050357  SHESHANK    REL-V7R5M0  5770-WDS
     A                                      WINDOW(7 21 10 35)
     A                                  2  2'Are You Sure You Want To Delete'
     A            WUSERID        5Y 0O  3 11EDTCDE(Z)
     A                                  5  2'Enter N to Cancel, else Enter Y'
     A            POPUPMSG      20A  O  9  2COLOR(WHT)
     A                                  8  2'F12 = Cancel'
     A                                      COLOR(BLU)
     A            WOPT           1A  B  6  2
     A  54                                  DSPATR(RI)
     A  54                                  DSPATR(PC)
     A                                  3  2'User Id:'
     A                                      COLOR(WHT)
     A          R PROMPT
     A*%%TS  SD  20251010  025037  SHESHANK    REL-V7R5M0  5770-WDS
     A                                      WINDOW(10 51 11 19)
     A                                  2  2'Select Department'
     A                                      COLOR(WHT)
     A                                  3  2'1. RPG'
     A                                  4  2'2. Java'
     A                                  5  2'3. EDI'
     A                                  7  2'Selection:'
     A                                      COLOR(WHT)
     A                                  9  2'F12=Cancel'
     A                                      COLOR(BLU)
     A            WSEL           1A  B  7 13
     A  55                                  DSPATR(RI)
     A  55                                  DSPATR(PC)
     A            PROMPTMSG     17A  O 10  2COLOR(WHT)
